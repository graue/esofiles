;; 99 bottles of beer on the wall
;; warning: alcohol destroys brain cells! (not as much as brainfuck)
;; written by Keymaker


  ++++[>+++++<-]>+++
  [
    [>+>+<<-]
    >++++++[<+>-]+++++++++[<++++++++++>-]
    >[<+>-]<-
  ]
  +++>+++++++++[<+++++++++>-]
  >++++++[<++++++++>-]<--[>+>+<<-]>>[<<+>>-]<--
  >>++++[<++++++++>-]++++++++++

  >+++++++++[>+++++++++++<-]>
  [
    [>+>+>+<<<-]>[<+>-]>>
    [
      [>+>+<<-]>>[<<+>>-]<
      [<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<<<+>---------->->[-]]]]]]]]]]]<
    ]
    <<[>>++++++++++++[<++++<++++>>-]<<[.[-]>]<<]
    >[<++++++[>++++++++<-]>.[-]]<<

    <<<.<<<<<.[<]>>>>>>>>>.<<<<<..>>>>>>>>.>>>>>>>.

    [>]>[>+>+<<-]>[<+>-]>[-[[-]+++++++++[<+++++++++++++>-]<--.[-]>]]<<

    <<<.[<]>>>>>>>>>.>>>>>>>>>.[>]<<.<<<<<.<<<..[<]>>>>>>.[>]<<.
    [<]>>>>>>>>>.>.[>]<<.[<]>>>>.>>>>>>>>>>>>.>>>.[>]<<.
    [<]>.[>]<<<<<<.<<<<<<<<<<<..[>]<<<.>.

    [>]>[>+>+>+<<<-]>[<+>-]>>
    [
      [>+>+<<-]>>[<<+>>-]<
      [<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<<<+>---------->->[-]]]]]]]]]]]<
    ]
    <<[>>++++++++++++[<++++<++++>>-]<<[.[-]>]<<]
    >[<++++++[>++++++++<-]>.[-]]<<

    <<<.<<<<<.[<]>>>>>>>>>.<<<<<..>>>>>>>>.>>>>>>>.

    [>]>[>+>+<<-]>[<+>-]>[-[[-]+++++++++[<+++++++++++++>-]<--.[-]>]]<<

    <<<.[<]>>>>>>>>>.>>>>>>>>>.[>]<<.<<<<<.<<<..[<]>>>>>>.[>]<<<<.>>>.

    <<<<.<.<<<<<<<<<<.>>>>>>.[>]<<.[<]>>>>>>>>>.>.>>>>>>>>>.[>]<<.
    <<<<<<<.[<]>>>>>>>>>.[<]>.>>>>>>>>>.[>]<<.<<<<.<<<<<<<<<<<<<.[>]<<<<<<<<<.
    [>]<<.[<]>>>>>>>>.[>]<<<<<<.[<]>>>>>..[>]<<.<<<<<<<<<<<<.[<]>>>>.
    [>]<<.<<<<.[<]>>>>>>.>>>.<<<<<<.>>>>>>>.>>>>>>>>>>.[>]<<<.>.

    >>>-

    [>+>+>+<<<-]>[<+>-]>>
    [
      [>+>+<<-]>>[<<+>>-]<
      [<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<->-[<<<+>---------->->[-]]]]]]]]]]]<
    ]
    <<[>>++++++++++++[<++++<++++>>-]<<[.[-]>]<<]
    >[<++++++[>++++++++<-]>.[-]]<<

    [>+>+<<-]>[<+>-]+>[<->[-]]<
    [-<<<[<]>>>>>>>>>>.<.[>]<<.[<]>>>>>>>>>>>.<<.<<<.[>]<<<<<<<<<<.[>]>>]<

    <<<.<<<<<.[<]>>>>>>>>>.<<<<<..>>>>>>>>.>>>>>>>.

    [>]>[>+>+<<-]>[<+>-]+>
    [<->-[<+>[-]]]<
    [++++++++[>+++++++++++++<-]>--.[-]<]<

    <<<.[<]>>>>>>>>>.>>>>>>>>>.[>]<<.<<<<<.<<<..[<]>>>>>>.[>]<<.
    [<]>>>>>>>>>.>.[>]<<.[<]>>>>.>>>>>>>>>>>>.>>>.[>]<<.
    [<]>.[>]<<<<<<.<<<<<<<<<<<..[>]<<<<.>>>..

    >>
  ]