;; text to morse code translator (using international morse code)
;; written by Keymaker
;; prints "end of message" prosign after printing out the actual message
;; remember to write in capitalized letters


  >>++++ ++++[<<++++>+++ +++>-]+<---[>+>+<<-]>>[<<+>>-]>

  ,
  [<+>
  [----------
  [----------------------
  [-
  [-
  [----
  [-
  [-
  [-
  [---
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [--
  [--
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-
  [-----
  <[<..<..>.<.>>-]>
  ]
  <[<<..>..>-]>
  ]
  <[<<.>.<..>>-]>
  ]
  <[<<.>..<.>>-]>
  ]
  <[<.<..>>-]>
  ]
  <[<...<.>>-]>
  ]
  <[<..<.>>-]>
  ]
  <[<<.>>-]>
  ]
  <[<...>-]>
  ]
  <[<.<.>.>-]>
  ]
  <[<<..>.<.>>-]>
  ]
  <[<.<..>.>-]>
  ]
  <[<<...>>-]>
  ]
  <[<<.>.>-]>
  ]
  <[<<..>>-]>
  ]
  <[<.<.>..>-]>
  ]
  <[<<.>.<.>>-]>
  ]
  <[<.<...>>-]>
  ]
  <[<..>-]>
  ]
  <[<....>-]>
  ]
  <[<<..>.>-]>
  ]
  <[<..<.>.>-]>
  ]
  <[<.>-]>
  ]
  <[<<.>..>-]>
  ]
  <[<<.>.<.>.>-]>
  ]
  <[<<.>...>-]>
  ]
  <[<.<.>>-]>
  ]
  <[<.<..>.<.>.>-]>
  ]
  <[<..<..>..>-]>
  ]
  <[<<.>...<.>>-]>
  ]
  <[<<.>.<.>.<.>.>-]>
  ]
  <[<<...>...>-]>
  ]
  <[<<....>.>-]>
  ]
  <[<<...>..>-]>
  ]
  <[<<..>...>-]>
  ]
  <[<<.>....>-]>
  ]
  <[<.....>-]>
  ]
  <[<....<.>>-]>
  ]
  <[<...<..>>-]>
  ]
  <[<..<...>>-]>
  ]
  <[<.<....>>-]>
  ]
  <[<<.....>>-]>
  ]
  <[<<.>..<.>.>-]>
  ]
  <[<.<.>.<.>.<.>>-]>
  ]
  <[<<.>....<.>>-]>
  ]
  <[<<..>..<..>>-]>
  ]
  <[<<.>.<..>.<.>>-]>
  ]
  <[<<.>.<..>.<.>>-]>
  ]
  <[<.<....>.>-]>
  ]
  <[<.<<.>>...>-]>
  ]
  <[<.<.>..<.>.>-]>
  ]
  <[<<.>.<.>.<..>>-]>
  ]
  <[<<<.....>>>-]>
  ]
  <[<<.>...<.>>-]>
  ]
  <<<<.>>>>,
  ]

  <<.<.>.<.>.