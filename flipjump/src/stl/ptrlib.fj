// Every line is (pointing) bananas!
// Implementation of pointers (variables which represents a memory-place), stack and functions.

// should be assembled with both bitlib.fj and runlib.fj files
// This file is independent of the bit-width, and uses the consts defined at runlib.fj

// Everything after // is ignored, and every whitespace is ignored (besides new line)
// An opcode is:  F;J  F;  ;J  or just  ;  (no F => F=0;  no J => J=next)

// Complexity note: phi is log2 of the total number of fj operations, counting wflips as one op.
// so 2 wflips with value=label (while label is padded with 2^n) causes phi-n space/time complexity

// The complexities are not updated in this file (should be lower/faster).



// ---------- Init


ns bit {
    def ptr_init {
        ._.ptr_init
    }

    ns _ {
        def ptr_init > to_flip, to_jump, to_flip_var, to_flip_return_var, to_jump_var {
            pad 1
          to_flip:
            0;0
          to_jump:
            ;0

          to_flip_var:
            bit.vec w, 0
          to_flip_return_var:
            bit.vec w, 0
          to_jump_var:
            bit.vec w, 0
        }
    }
}



// ---------- Jump:


ns bit {
    // like:  ;*ptr
    // Complexity w(2phi+6)
    def ptr_jump ptr < ._.to_jump, ._.to_jump_var {
        .bit_var_xor w, ._.to_jump+w, ._.to_jump_var, ._.to_jump_var
        .bit_var_xor w, ._.to_jump+w, ._.to_jump_var, ptr
        ;._.to_jump
    }
}



// ---------- Flip:


ns bit {
    // like:  *ptr;
    // Complexity w(2phi+6)
    def ptr_flip ptr @ cleanup < ._.to_flip, ._.to_flip_var {
        wflip ._.to_flip+w, cleanup

        .bit_var_xor w, ._.to_flip, ._.to_flip_var, ._.to_flip_var
        .bit_var_xor w, ._.to_flip, ._.to_flip_var, ptr
        ;._.to_flip

      cleanup:
        wflip ._.to_flip+w, cleanup
    }

    // like:  (*ptr)+dbit;
    // Assumes *ptr is dw-aligned
    // Complexity w(2phi+7)
        // The comp_flip_if executes much less than w/2 operations.
    def ptr_flip_dbit ptr {
        rep(#dbit, i) comp_flip_if ptr+dbit+i*dw, (dbit>>i)&1
        .ptr_flip ptr
        rep(#dbit, i) comp_flip_if ptr+dbit+i*dw, (dbit>>i)&1
    }

    // Assumes *ptr is dw-aligned
    def xor_to_ptr ptr, bit @ end {
        .if0 bit, end
        .ptr_flip_dbit ptr
      end:
    }


    // like:  wflip *ptr, value
    // Assumes *ptr is w-aligned, and value is 2dw-aligned
    // Complexity w(3phi+11)
    def ptr_wflip ptr, value < ._.to_flip, ._.to_flip_var{
        .bit_var_xor w, ._.to_flip, ._.to_flip_var, ._.to_flip_var
        .bit_var_xor w, ._.to_flip, ._.to_flip_var, ptr
        rep(w, i) ._.advance_by_one_and_flip__ptr_wflip (#(i^((i+1)%w))), (value>>i)&1
    }
    ns _ {
        def advance_by_one_and_flip__ptr_wflip n, do_flip @ cleanup, advance < .to_flip {
            comp_if0 do_flip, advance
            wflip .to_flip+w, cleanup, .to_flip
          cleanup:
            wflip .to_flip+w, cleanup
          advance:
            rep(n, i) ..exact_not .to_flip+i
        }
    }

    // Assumes *ptr is dw-aligned, and value is 2dw-aligned
    def ptr_wflip_2nd_word ptr, value {
        .not ptr + dw*ww
        .ptr_wflip ptr, value
        .not ptr + dw*ww
    }
}



// ---------- Xor


ns bit {
    // like:  .xor dst *ptr
    // assumes *ptr is dw-aligned
    // Complexity w(8phi+28)
    def xor_from_ptr dst, ptr {
        .exact_xor_from_ptr dst+dbit, ptr
    }

    def exact_xor_from_ptr dst, ptr @ base_jump_label, cleanup {
        .ptr_wflip_2nd_word ptr, base_jump_label

        .ptr_jump ptr
        pad 2
      base_jump_label:
        ;cleanup
        dst;

      cleanup:
        .ptr_wflip_2nd_word ptr, base_jump_label

    }
}



// ---------- Stack


ns bit {
    def stack n {
        ._.stack n
    }
    ns _ {
        // sp always points to the last pushed value (at start - to stack[-1])
        def stack n > sp, stack{
            pad 1
          sp:
            bit.vec w, .stack-dw
          stack:
            bit.vec n, 0
        }
    }
    def get_sp dst < ._.sp {
        .mov w, dst, ._.sp
    }


    // Complexity w(2phi+10)
    def inc_ptr ptr {
        .inc w-dww, ptr+dww*dw
    }


    // Complexity w(2phi+12)
    def dec_ptr ptr {
        .dec w-dww, ptr+dww*dw
    }


    // Assumes address is 2dw-aligned
    // Complexity w(5phi+21)
    def push_ret_address address < ._.sp {
        .inc_ptr ._.sp
        .ptr_wflip_2nd_word ._.sp, address
    }

    // Assumes address is 2dw-aligned
    // Complexity w(5phi+23)
    def pop_ret_address address < ._.sp {
        .ptr_wflip_2nd_word ._.sp, address
        .dec_ptr ._.sp
    }


    // Complexity: w(4phi+17)
    def push bit < ._.sp {
        .inc_ptr ._.sp
        .xor_to_ptr ._.sp, bit
    }

    // Assumes *sp==bit.
    // Complexity: w(4phi+19)
    def pop bit < ._.sp {
        .xor_to_ptr ._.sp, bit
        .dec_ptr ._.sp
    }

    // Complexity: w(12phi+47)
    def pop_res bit < ._.sp {
        .zero bit
        .xor_from_ptr bit, ._.sp
        .pop bit
    }
}



// ---------- Functions


ns bit {
        // Complexity: w(5phi+21)
        // the pop_ret_address is counted for the future return
    def call address @ return_label {
        .push_ret_address return_label
        ;address

        pad 2
      return_label:
        .pop_ret_address return_label
    }


    // Complexity: w(7phi+29)
        // the last-call's pop_ret_address is counted for this return
    def return < ._.sp {
        .ptr_jump ._.sp
    }
}



// ---------- Fast Call


def fcall_init {
    _.fcall_init
}
ns _ {
    def fcall_init > ret_addr1, ret_addr2, ret_addr3 {
        ret_addr1: bit 0
        ret_addr2: bit 0
        ret_addr3: bit 0
    }
}

// Complexity: phi+1
def fcall label, ret_reg @ ret {
    wflip ret_reg+w, ret, label
    pad 2
  ret:
    wflip ret_reg+w, ret
}

// Complexity: 1
def fret ret_reg {
    ;ret_reg
}

def fcall1 label < _.ret_addr1 {
    fcall label, _.ret_addr1
}
def fcall2 label < _.ret_addr2 {
    fcall label, _.ret_addr2
}
def fcall3 label < _.ret_addr3 {
    fcall label, _.ret_addr3
}
def fret1 < _.ret_addr1 {
    fret _.ret_addr1
}
def fret2 < _.ret_addr2 {
    fret _.ret_addr2
}
def fret3 < _.ret_addr3 {
    fret _.ret_addr3
}
